!
!  Calculate Cv curve from weights and visit statistics
!  for replicas at different temperatures REPT(J1).
!  Minimise chi^2 statistic to extract best probabilities.
!
PROGRAM CALCCV
IMPLICIT NONE
DOUBLE PRECISION TMIN, TMAX, TINT, Z0, Z1, Z2, TEMPERATURE, DUMMY, DELTA, ONEMEXP, PSUM, DP, PSUM2, TANAL, DPLUS, DMINUS
DOUBLE PRECISION COLOURTHRESH
DOUBLE PRECISION, ALLOCATABLE :: ENERGY(:), LNFRQS(:), MEANE(:), ZSAVE(:), PERMINP(:), PERMINM(:)
INTEGER, ALLOCATABLE :: HORDER(:), MINP(:), MINM(:)
INTEGER J1, NTEMP, J2, KAPPA, NMIN, NPLUS, NMINUS
LOGICAL YESNO

OPEN(UNIT=10,FILE='Cv.data',STATUS='OLD')
READ(10,*) TMIN, TMAX, NTEMP, KAPPA, NMIN
CLOSE(10)
ALLOCATE(MEANE(NTEMP),ZSAVE(NTEMP))

PRINT '(3(A,I10))','minima=',NMIN
PRINT '(A,2G15.5,A,I8)','minimum and maximum T for Cv calculation=',TMIN,TMAX,' number of Cv data points=',NTEMP
PRINT '(A,I8)','number of degrees of freedom=',KAPPA

ALLOCATE(ENERGY(NMIN),HORDER(NMIN),LNFRQS(NMIN))

OPEN(UNIT=10,FILE='min.data',STATUS='OLD')
DO J1=1,NMIN
   READ(10,*) ENERGY(J1),LNFRQS(J1),HORDER(J1)
ENDDO
CLOSE(10)

TINT=(TMAX-TMIN)/(NTEMP-1)
!
!  Calculate Z0, Z1 and Z2 over the required T range. Omit factors of (kT/h)^kappa,
!  which cancel.
!
OPEN(UNIT=1,FILE='Cv.out',STATUS='UNKNOWN')
DO J1=1,NTEMP
   Z0=0.0D0
   Z1=0.0D0
   Z2=0.0D0
   TEMPERATURE=TMIN+(J1-1)*TINT
   DO J2=1,NMIN
      DUMMY=EXP(-(ENERGY(J2)-ENERGY(1))/TEMPERATURE+(LNFRQS(1)-LNFRQS(J2))/2.0D0)/HORDER(J2)
      Z0=Z0+DUMMY
      Z1=Z1+DUMMY*(ENERGY(J2)-ENERGY(1))
      Z2=Z2+DUMMY*(ENERGY(J2)-ENERGY(1))**2
   ENDDO
   MEANE(J1)=Z1/Z0
   ZSAVE(J1)=Z0
   WRITE(1,'(7G20.10)') TEMPERATURE, Z0, Z1, Z2, &
  &                     KAPPA - (Z1/(Z0*TEMPERATURE))**2 + Z2/(Z0*TEMPERATURE**2), MEANE(J1), ZSAVE(J1)
ENDDO
CLOSE(1)

OPEN(UNIT=1,FILE='prob.out',STATUS='UNKNOWN')
DO J1=1,NTEMP
   TEMPERATURE=TMIN+(J1-1)*TINT
   PSUM=0.0D0
   PSUM2=0.0D0
   DO J2=1,NMIN
      DUMMY=EXP(-(ENERGY(J2)-ENERGY(1))/TEMPERATURE+(LNFRQS(1)-LNFRQS(J2))/2.0D0)/HORDER(J2)
      DP=DUMMY*(ENERGY(J2)-ENERGY(1)-MEANE(J1))/(ZSAVE(J1)*TEMPERATURE**2)
      PSUM=PSUM+DP*(ENERGY(J2)-ENERGY(1)-MEANE(J1))
!     PRINT '(A,2I6,4G20.10)','J1,J2,T,ZSAVE,DUMMY,DP=',J1,J2,TEMPERATURE,ZSAVE(J1),DUMMY,DP
      PSUM2=PSUM2+(ZSAVE(J1)/DUMMY)*(TEMPERATURE*DP)**2
   ENDDO
   WRITE(1,'(5G20.10)') TEMPERATURE, KAPPA+PSUM, KAPPA+PSUM2
ENDDO
CLOSE(1)

INQUIRE(FILE='Tanal',EXIST=YESNO)

IF (YESNO) THEN
   NPLUS=0.0D0
   NMINUS=0.0D0
   DPLUS=0.0D0
   DMINUS=0.0D0
   ALLOCATE(PERMINP(NMIN),PERMINM(NMIN),MINP(NMIN),MINM(NMIN))
   OPEN(UNIT=1,FILE='Tanal')
   READ(1,*) TANAL
   COLOURTHRESH=0.9D0
   READ(1,*,END=666) COLOURTHRESH
666 CONTINUE
   PRINT '(2(A,G20.10))','Analysing heat capacity contributions at T=',TANAL,' colour threshold=',COLOURTHRESH
   CLOSE(1)
   TEMPERATURE=TANAL
   Z0=0.0D0
   Z1=0.0D0
   DO J2=1,NMIN
      DUMMY=EXP(-(ENERGY(J2)-ENERGY(1))/TEMPERATURE+(LNFRQS(1)-LNFRQS(J2))/2.0D0)/HORDER(J2)
      Z0=Z0+DUMMY
      Z1=Z1+DUMMY*(ENERGY(J2)-ENERGY(1))
   ENDDO
   MEANE(1)=Z1/Z0
   ZSAVE(1)=Z0
   DO J2=1,NMIN
      DUMMY=EXP(-(ENERGY(J2)-ENERGY(1))/TEMPERATURE+(LNFRQS(1)-LNFRQS(J2))/2.0D0)/HORDER(J2)
      DP=DUMMY*(ENERGY(J2)-ENERGY(1)-MEANE(1))/(ZSAVE(1)*TEMPERATURE**2)
      DUMMY=DP*(ENERGY(J2)-ENERGY(1)-MEANE(1))
      IF (DP.LT.0.0D0) THEN
         NMINUS=NMINUS+1
         MINM(NMINUS)=J2
         PERMINM(NMINUS)=DUMMY
         DMINUS=DMINUS+DUMMY
      ELSE
         NPLUS=NPLUS+1
         MINP(NPLUS)=J2
         PERMINP(NPLUS)=DUMMY
         DPLUS=DPLUS+DUMMY
      ENDIF
!     PRINT '(A,I6,3G20.10,2I6)','J2,ENERGY(J2)-ENERGY(1),MEANE,DP,NPLUS,NMINUS=',J2,ENERGY(J2)-ENERGY(1),MEANE(1),DP,NPLUS,NMINUS
   ENDDO
   DO J2=1,NMINUS
      PERMINM(J2)=PERMINM(J2)/DMINUS
   ENDDO
   DO J2=1,NPLUS
      PERMINP(J2)=PERMINP(J2)/DPLUS
   ENDDO
   CALL SORT(NMINUS,NMIN,PERMINM,MINM)
   CALL SORT(NPLUS,NMIN,PERMINP,MINP)
   DUMMY=0.0D0
   PRINT '(A)','minima with negative dp/dT:'
   DO J2=1,NMINUS
      DUMMY=DUMMY+PERMINM(J2)
      PRINT '(2I6,2G20.10)',J2,MINM(J2),PERMINM(J2),DUMMY
   ENDDO
   DUMMY=0.0D0
   PRINT '(A)','minima with positive dp/dT:'
   DO J2=1,NPLUS
      DUMMY=DUMMY+PERMINP(J2)
      PRINT '(2I6,2G20.10)',J2,MINP(J2),PERMINP(J2),DUMMY
   ENDDO
   PRINT '(A,I8,A)','For disconnectionDPS use line TRMIN 2 ',NMIN,' min.minus min.plus and '
   PRINT '(A)','CHOOSECOLOURS in the dinfo file'
   OPEN(UNIT=1,FILE='min.minus',STATUS='UNKNOWN')
   DUMMY=0.0D0
   DO J2=1,NMINUS
      DUMMY=DUMMY+PERMINM(J2)
      WRITE(1,'(I6)') MINM(J2)
      IF (DUMMY.GT.COLOURTHRESH) EXIT
   ENDDO
   CLOSE(1)
   OPEN(UNIT=1,FILE='min.plus',STATUS='UNKNOWN')
   DUMMY=0.0D0
   DO J2=1,NPLUS
      DUMMY=DUMMY+PERMINP(J2)
      WRITE(1,'(I6)') MINP(J2)
      IF (DUMMY.GT.COLOURTHRESH) EXIT
   ENDDO
   CLOSE(1)
ENDIF

END PROGRAM CALCCV

SUBROUTINE SORT(N,J3,A,NA)
IMPLICIT NONE
INTEGER J1, L, N, J3, J2, NA(J3), NTEMP
DOUBLE PRECISION TEMP, A(J3)
!
DO 20 J1=1,N-1
   L=J1
   DO 10 J2=J1+1,N
      IF (A(L).LT.A(J2)) L=J2
10 CONTINUE
   TEMP=A(L)
   A(L)=A(J1)
   A(J1)=TEMP
   NTEMP=NA(L)
   NA(L)=NA(J1)
   NA(J1)=NTEMP
20 CONTINUE
RETURN
END SUBROUTINE SORT

