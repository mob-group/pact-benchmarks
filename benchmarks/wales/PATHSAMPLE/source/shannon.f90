!   PATHSAMPLE: A driver for OPTIM to create stationary point databases using discrete path sampling 
!   and perform kinetic analysis
!   Copyright (C) 1999-2009 David J. Wales
!   This file is part of PATHSAMPLE.
!
!   PATHSAMPLE is free software; you can redistribute it and/or modify
!   it under the terms of the GNU General Public License as published by
!   the Free Software Foundation; either version 2 of the License, or
!   (at your option) any later version.
!
!   PATHSAMPLE is distributed in the hope that it will be useful,
!   but WITHOUT ANY WARRANTY; without even the implied warranty of
!   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!   GNU General Public License for more details.
!
!   You should have received a copy of the GNU General Public License
!   along with this program; if not, write to the Free Software
!   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
!
SUBROUTINE SHANNON
USE PORFUNCS
USE UTILS
USE SAVESTATE
USE COMMONS, ONLY : DEBUG, KAPPA, TOTALE, TEMPERATURE, ENSEMBLE, HORDERMIN, FVIBMIN, TOTALE, NMIN, EMIN, &
  &                 SHANNONTMIN, SHANNONTMAX, SHANNONTINC, EINC, NPEQ, NMINA, NMINB, LOCATIONA, LOCATIONB, &
  &                 TSATTEMPT, MAXTS, NTS, MINGROUP, FVIBTS, HORDERTS, NEGEIG, REGROUPFREETHRESH, GPFOLD, &
  &                 IXMIN, IYMIN, IZMIN, POINTERP, POINTERM,  MINUS, PLUS, TOPPOINTER, KMINUS,  KPLUS, &
  &                 ETS, PFMIN, ZSYM, PI, PFMEAN, PFTOTALA, PFTOTALB, FRICTIONT, RATEAB, RATEBA, &
  &                 NRATESCYCLETEMPS, RATESCYCLETEMPS, NCONNMAX, SHANNONRT, &
  &                 SHANNONZT, EDIFFTOL
IMPLICIT NONE
INTEGER J1, LUNIT, NGMIN, NHIGHESTPEQ(NPEQ), J2, J3, J4, NSTEPS, NDUMMY, NCONNMAXSAVE, NGMINCOPIES
DOUBLE PRECISION PFNORM1, PFNORM2, BARRIER(NMIN), DUMMY, FRUSTRATION, HIGHESTPEQ(NPEQ)
DOUBLE PRECISION DUMMY2, RATESUM1, RATESUM2, PRENORM
DOUBLE PRECISION GLOBALMIN, SENTROPY, TEMPSAVE
DOUBLE PRECISION FRICTIONFAC, PSUM
INTEGER, ALLOCATABLE :: MINMAP(:)

ALLOCATE(TSATTEMPT(MAXTS))
TEMPSAVE=TEMPERATURE
!
!  Save state.
!
IF (ALLOCATED(MINGROUP)) DEALLOCATE(MINGROUP)
ALLOCATE(EMINSAVE(NMIN),PFMINSAVE(NMIN),ETSSAVE(NTS),KPLUSSAVE(NTS),KMINUSSAVE(NTS),TOPPOINTERSAVE(NMIN), &
  &         PLUSSAVE(NTS),MINUSSAVE(NTS),POINTERMSAVE(NTS),POINTERPSAVE(NTS),MINGROUP(NMIN), &
  &         LOCATIONASAVE(NMINA),LOCATIONBSAVE(NMINB))
ALLOCATE(FVIBMINSAVE(NMIN),HORDERMINSAVE(NMIN),IXMINSAVE(NMIN),IYMINSAVE(NMIN),IZMINSAVE(NMIN), &
         NCONNSAVE(NMIN),GPFOLDSAVE(NMIN))
NMINASAVE=NMINA; NMINBSAVE=NMINB; NMINSAVE=NMIN; NTSSAVE=NTS; LOCATIONASAVE(1:NMINA)=LOCATIONA(1:NMINA)
LOCATIONBSAVE(1:NMINB)=LOCATIONB(1:NMINB); EMINSAVE(1:NMIN)=EMIN(1:NMIN); PFMINSAVE(1:NMIN)=PFMIN(1:NMIN)
ETSSAVE(1:NTS)=ETS(1:NTS); KPLUSSAVE(1:NTS)=KPLUS(1:NTS); KMINUSSAVE(1:NTS)=KMINUS(1:NTS)
TOPPOINTERSAVE(1:NMIN)=TOPPOINTER(1:NMIN); PLUSSAVE(1:NTS)=PLUS(1:NTS); MINUSSAVE(1:NTS)=MINUS(1:NTS)
POINTERMSAVE(1:NTS)=POINTERM(1:NTS); POINTERPSAVE(1:NTS)=POINTERP(1:NTS)
PFMEANSAVE=PFMEAN; PFTOTALASAVE=PFTOTALA; PFTOTALBSAVE=PFTOTALB
FVIBMINSAVE(1:NMIN)=FVIBMIN(1:NMIN); HORDERMINSAVE(1:NMIN)=HORDERMIN(1:NMIN)
IXMINSAVE(1:NMIN)=IXMIN(1:NMIN); IYMINSAVE(1:NMIN)=IYMIN(1:NMIN); IZMINSAVE(1:NMIN)=IZMIN(1:NMIN)

CALL GETBARRIER2(BARRIER,NGMIN,GLOBALMIN)
NGMINCOPIES=0
DO J1=1,NMIN
   IF (ABS(EMIN(J1)-GLOBALMIN).LT.EDIFFTOL) NGMINCOPIES=NGMINCOPIES+1
   IF (BARRIER(J1).LT.0.0D0) THEN
     BARRIER(J1)=0.0D0
     CYCLE
   ENDIF
   IF (J1.EQ.NGMIN) CYCLE
   IF (ABS(EMIN(J1)-GLOBALMIN).LT.EDIFFTOL) CYCLE ! allow for degeneracy
   BARRIER(J1)=BARRIER(J1)/ABS(EMIN(J1)-GLOBALMIN)
ENDDO
PRINT '(A,I8,A)','shannon> ',NGMINCOPIES,' entries for global minimum detected in min.data'
!
! BARRIER contains barrier divided by PE difference from lowest minimum.
!
!  Calculate partition functions for minima. Note that the total partition function
!  is not needed, just the totals for A and B. Since A and B sets are fixed here
!  we don;t need to change the totals.
!
      
GLOBALMIN=HUGE(1.0D0)
DO J1=1,NMIN
   IF (EMIN(J1).LT.GLOBALMIN) THEN
      NGMIN=J1
      GLOBALMIN=EMIN(J1)
   ENDIF
ENDDO
PRINT '(A,I10,G20.10)','setup> Lowest minimum position and energy=',NGMIN,GLOBALMIN
PRINT '(A,G20.10)','setup> Shifting all potential energies relative to zero for the lowest minimum'
DO J1=1,NMIN
   EMIN(J1)=EMIN(J1)-GLOBALMIN
ENDDO
GLOBALMIN=0.0D0

PRINT '(A,2F15.5,2(A,F20.10))','shannon> Calculating Shannon entropy for temperature range ', &
  &       SHANNONTMIN,SHANNONTMAX,' increment ',SHANNONTINC,' energy increment for barriers=',EINC
LUNIT=GETUNIT()
OPEN(LUNIT,FILE='Shannon.out',STATUS='UNKNOWN')
TEMPERATURE=SHANNONTMIN
20 CONTINUE

PFMEAN=-HUGE(1.0D0)
PFNORM1=0.0D0 ! use this to calculate ratios without the pe factor
PFNORM2=0.0D0 ! use this to calculate ratios with the pe factor

IF (ENSEMBLE.EQ.'T') THEN
!
! Note that common factors are normally omitted from the canonical partition
! function. For example, the temperature^kappa
!
   DO J1 = 1,NMIN
      PFMIN(J1) = -EMIN(J1)/TEMPERATURE - FVIBMIN(J1)/2.0D0 - LOG(1.0D0*HORDERMIN(J1))
      IF (PFMIN(J1).GT.PFMEAN) PFMEAN=PFMIN(J1)
   ENDDO
   DO J1 = 1,NMIN
      PFMIN(J1) = -EMIN(J1)/TEMPERATURE - FVIBMIN(J1)/2.0D0 - LOG(1.0D0*HORDERMIN(J1)) - PFMEAN
      PFNORM1=PFNORM1+EXP(- FVIBMIN(J1)/2.0D0 - LOG(1.0D0*HORDERMIN(J1))+ FVIBMIN(1)/2.0D0 + LOG(1.0D0*HORDERMIN(1)))
      PFNORM2=PFNORM2+EXP(PFMIN(J1)-PFMIN(1))
   ENDDO
ELSEIF (ENSEMBLE.EQ.'E') THEN
   DO J1 = 1,NMIN
      IF (TOTALE.GT.EMIN(J1)) THEN
         PFMIN(J1) = (KAPPA-1)*LOG(TOTALE-EMIN(J1)) - FVIBMIN(J1)/2.0D0 - LOG(1.0D0*HORDERMIN(J1))
         PFNORM1=PFNORM1+EXP(- FVIBMIN(J1)/2.0D0 - LOG(1.0D0*HORDERMIN(J1)) + FVIBMIN(1)/2.0D0 + LOG(1.0D0*HORDERMIN(1)))
         PFNORM2=PFNORM2+EXP(PFMIN(J1)-PFMIN(1))
         IF (PFMIN(J1).GT.PFMEAN) PFMEAN=PFMIN(J1)
      ELSE
         PFMIN(J1) = -1.0D250
      ENDIF
   ENDDO
ENDIF
PFTOTALB=0.0D0
DO J2=1,NMINB
   PFTOTALB=PFTOTALB+EXP(PFMIN(LOCATIONB(J2))-PFMIN(LOCATIONB(1)))
ENDDO
IF (NMINB.GT.0.0D0) PFTOTALB=LOG(PFTOTALB)+PFMIN(LOCATIONB(1))

PFTOTALA=0.0D0
DO J2=1,NMINA
   PFTOTALA=PFTOTALA+EXP(PFMIN(LOCATIONA(J2))-PFMIN(LOCATIONA(1)))
ENDDO
IF (NMINA.GT.0.0D0) PFTOTALA=LOG(PFTOTALA)+PFMIN(LOCATIONA(1))
!
!  Calculate rate constants for this temperature. The original values
!  have been saved in PLUSSAVE and MINUSSAVE above.
!
IF (SHANNONRT) THEN
IF (ENSEMBLE.EQ.'T') THEN
   DO J2=1,NTS
      KPLUS(J2)  = LOG(1.0D0 * HORDERMIN(PLUS(J2))  / (2.0D0 * PI*HORDERTS(J2))) + &
  &          (FVIBMIN(PLUS(J2))  - FVIBTS(J2)) / 2.0D0 - (ETS(J2) - EMIN(PLUS(J2)) )/TEMPERATURE
      IF (FRICTIONT) KPLUS(J2)=KPLUS(J2)+LOG(FRICTIONFAC(NEGEIG(J2)))
         KMINUS(J2) = LOG(1.0D0 * HORDERMIN(MINUS(J2)) / (2.0D0 * PI*HORDERTS(J2))) + &
  &          (FVIBMIN(MINUS(J2)) - FVIBTS(J2)) / 2.0D0 - (ETS(J2) - EMIN(MINUS(J2)))/TEMPERATURE
      IF (FRICTIONT) KMINUS(J2)=KMINUS(J2)+LOG(FRICTIONFAC(NEGEIG(J2)))
      IF (ZSYM(1:2).EQ.'CA') KPLUS(J2)=KPLUS(J2)+30.66356D0
      IF (ZSYM(1:2).EQ.'CA') KMINUS(J2)=KMINUS(J2)+30.66356D0
      IF (PLUS(J2).EQ.MINUS(J2)) KPLUS(J2)=KPLUS(J2)+LOG(2.0D0)
      IF (PLUS(J2).EQ.MINUS(J2)) KMINUS(J2)=KMINUS(J2)+LOG(2.0D0)
   ENDDO
ELSE
   DO J2=1,NTS
      IF (TOTALE.GT.ETS(J2)) THEN
         KPLUS(J2)  = LOG(1.0D0 * HORDERMIN(PLUS(J2))  / (2*PI*HORDERTS(J2))) + &
  &         (FVIBMIN(PLUS(J2))  - FVIBTS(J2))/2 + (KAPPA-1)*LOG((TOTALE-ETS(J2))/(TOTALE-EMIN(PLUS(J2))))
         KMINUS(J2) = LOG(1.0D0 * HORDERMIN(MINUS(J2)) / (2*PI*HORDERTS(J2))) + &
  &        (FVIBMIN(MINUS(J2)) - FVIBTS(J2))/2 + (KAPPA-1)*LOG((TOTALE-ETS(J2))/(TOTALE-EMIN(MINUS(J2))))
         IF (ZSYM(1:2).EQ.'CA') KPLUS(J2)=KPLUS(J2)+30.66356D0
         IF (ZSYM(1:2).EQ.'CA') KMINUS(J2)=KMINUS(J2)+30.66356D0
         IF (PLUS(J2).EQ.MINUS(J2)) KPLUS(J2)=KPLUS(J2)+LOG(2.0D0)
         IF (PLUS(J2).EQ.MINUS(J2)) KMINUS(J2)=KMINUS(J2)+LOG(2.0D0)
      ELSE
         KPLUS(J2)=-1.0D250
         KMINUS(J2)=-1.0D250
      ENDIF
   ENDDO
ENDIF
ENDIF
IF (DEBUG) WRITE(*,'(A,3G20.10)') 'setup> largest ln Z,PFNORM1,PFNORM2=',PFMEAN,PFNORM1,PFNORM2
IF (DEBUG) WRITE(*,'(A)') '        V-V_min    pg order     high T/E prob       Peq'
SENTROPY=0.0D0
FRUSTRATION=0.0D0
PRENORM=0.0D0
HIGHESTPEQ(1:NPEQ)=-1.0D0
PSUM=0.0D0
DO J1=1,NMIN
   DUMMY=EXP(PFMIN(J1)-PFMIN(1)-LOG(PFNORM2))
   DUMMY2=EXP(PFMIN(J1)-PFMIN(1)-LOG(PFNORM2))
   IF (DEBUG) WRITE(*,'(F20.10,I6,2G20.10)') EMIN(J1),HORDERMIN(J1),  &
  &              EXP(-FVIBMIN(J1)/2.0D0-LOG(1.0D0*HORDERMIN(J1))-LOG(PFNORM1)+ FVIBMIN(1)/2.0D0 + LOG(1.0D0*HORDERMIN(1))),  &
  &              DUMMY
!  IF(BARRIER(J1).GT.0.0D0.OR.J1.EQ.NGMIN) SENTROPY=SENTROPY+DUMMY*LOG(DUMMY)
   SENTROPY=SENTROPY+DUMMY*LOG(DUMMY)
   IF(BARRIER(J1).GT.0.0D0.OR.J1.EQ.NGMIN) PSUM=PSUM+DUMMY
   PRINT '(A,I10,3G20.10)','J1,emina,globalmin,edifftol=',J1,EMIN(J1),GLOBALMIN,EDIFFTOL
   IF (ABS(EMIN(J1)-GLOBALMIN).GT.EDIFFTOL) THEN
      FRUSTRATION=FRUSTRATION+DUMMY*BARRIER(J1)
!     IF (BARRIER(J1).GT.0.0D0) PRENORM=PRENORM+DUMMY
      PRENORM=PRENORM+DUMMY
      PRINT '(A,I10,2G20.10)','J1,PRENORM,DUMMY=',J1,PRENORM,DUMMY
   ENDIF
!
! Maintain ordered list of the NPEQ highest probability minima at this temperature
!
   sortloop: DO J3=1,NPEQ ! sort to find the largest barriers to product
      IF (DUMMY.GT.HIGHESTPEQ(J3)) THEN
         DO J4=NPEQ,J3+1,-1
            NHIGHESTPEQ(J4)=NHIGHESTPEQ(J4-1)
            HIGHESTPEQ(J4)=HIGHESTPEQ(J4-1)
         ENDDO
         NHIGHESTPEQ(J3)=J1
         HIGHESTPEQ(J3)=DUMMY
         EXIT sortloop
      ENDIF
   ENDDO sortloop
ENDDO
SENTROPY=-SENTROPY
PRINT *,'PSUM=',PSUM
!
! Dijkstra analysis for number of steps between the most probable NPEQ minima.
!
IF (.NOT.ALLOCATED(MINMAP)) THEN ! allow for the case of no regrouping
   ALLOCATE(MINMAP(NMIN))
   DO J1=1,NMIN
      MINMAP(J1)=J1
   ENDDO
ENDIF

DUMMY=0.0D0
DUMMY2=0.0D0
RATESUM1=0.0D0
RATESUM2=0.0D0
NDUMMY=0
IF (NTS.EQ.0) GOTO 975
IF (.NOT.SHANNONRT) GOTO 975
DO J2=1,NPEQ
   DO J3=J2+1,NPEQ
      NMINA=1
      NMINB=1
      LOCATIONA(1)=NHIGHESTPEQ(J2)
      LOCATIONB(1)=NHIGHESTPEQ(J3)
      TSATTEMPT(1:NTS)=0
!     PRINT '(A,3I6)','doing minima=',NHIGHESTPEQ(J2),NHIGHESTPEQ(J3)
      NCONNMAXSAVE=NCONNMAX
      CALL DIJKSTRA(NSTEPS,.FALSE.,0,NMINSAVE,MINMAP)
      IF (NSTEPS.EQ.10000000) THEN
         PRINT '(A,2I6)','shannon> WARNING *** no connection between minima ',NHIGHESTPEQ(J2),NHIGHESTPEQ(J3)
      ELSE
         DUMMY=DUMMY+(HIGHESTPEQ(J2)+HIGHESTPEQ(J3))*NSTEPS
         DUMMY2=DUMMY2+NSTEPS
         NDUMMY=NDUMMY+1
         CALL NGT
         PRINT '(A,3I6,A,2G20.10,A,F12.5)','minima, number of steps: ',NHIGHESTPEQ(J2),NHIGHESTPEQ(J3),NSTEPS, &
  &                      ' rates: ',RATEAB,RATEBA,' T=',TEMPERATURE
         RATESUM1=RATESUM1+HIGHESTPEQ(J2)*RATEBA+HIGHESTPEQ(J3)*RATEAB
         RATESUM2=RATESUM2+RATEBA+RATEAB
      ENDIF
!
! Reset everything - necessary after NGT
!
      IF (ALLOCATED(LOCATIONA)) DEALLOCATE(LOCATIONA)
      IF (ALLOCATED(LOCATIONB)) DEALLOCATE(LOCATIONB)
      ALLOCATE(LOCATIONA(NMINASAVE),LOCATIONB(NMINBSAVE))
      NCONNMAX=NCONNMAXSAVE; NMINA=NMINASAVE; NMINB=NMINBSAVE; NMIN=NMINSAVE; NTS=NTSSAVE; LOCATIONA(1:NMINA)=LOCATIONASAVE(1:NMINA)
      LOCATIONB(1:NMINB)=LOCATIONBSAVE(1:NMINB); EMIN(1:NMIN)=EMINSAVE(1:NMIN); PFMIN(1:NMIN)=PFMINSAVE(1:NMIN)
      ETS(1:NTS)=ETSSAVE(1:NTS); KPLUS(1:NTS)=KPLUSSAVE(1:NTS); KMINUS(1:NTS)=KMINUSSAVE(1:NTS)
      TOPPOINTER(1:NMIN)=TOPPOINTERSAVE(1:NMIN); PLUS(1:NTS)=PLUSSAVE(1:NTS); MINUS(1:NTS)=MINUSSAVE(1:NTS)
      POINTERM(1:NTS)=POINTERMSAVE(1:NTS); POINTERP(1:NTS)=POINTERPSAVE(1:NTS)
      PFMEAN=PFMEANSAVE; PFTOTALA=PFTOTALASAVE; PFTOTALB=PFTOTALBSAVE
      FVIBMIN(1:NMIN)=FVIBMINSAVE(1:NMIN); HORDERMIN(1:NMIN)=HORDERMINSAVE(1:NMIN)
      IXMIN(1:NMIN)=IXMINSAVE(1:NMIN); IYMIN(1:NMIN)=IYMINSAVE(1:NMIN); IZMIN(1:NMIN)=IZMINSAVE(1:NMIN)
      GPFOLD(1:NMIN)=GPFOLDSAVE(1:NMIN)
   ENDDO
ENDDO

PRINT '(A,G20.10)','shannon> PRENORM=',PRENORM
DUMMY2=DUMMY2/NDUMMY
RATESUM2=RATESUM2/NDUMMY
975 CONTINUE ! jump here if the database is minima only, or we do not want to calculate rates
WRITE(*,'(A,8G14.5)') 'PRENORM,FRUSTRATION,FRUSTRATION/PRENORM=',PRENORM,FRUSTRATION,FRUSTRATION/PRENORM
IF (SHANNONRT) THEN
WRITE(LUNIT,'(8G14.5)') TEMPERATURE,SENTROPY,FRUSTRATION,FRUSTRATION/PRENORM,DUMMY,DUMMY2,RATESUM1,RATESUM2
ELSE
WRITE(LUNIT,'(8G14.5)') TEMPERATURE,SENTROPY,FRUSTRATION,FRUSTRATION/PRENORM,1.0D0-PRENORM
ENDIF
PRINT *,'highest ',NPEQ,' Peq minima:'
WRITE(*,'(2I6,G20.10)') (J2,NHIGHESTPEQ(J2),HIGHESTPEQ(J2),J2=1,NPEQ)

TEMPERATURE=TEMPERATURE+SHANNONTINC

IF (TEMPERATURE.GT.SHANNONTMAX) THEN
   CLOSE(LUNIT)
   DEALLOCATE(EMINSAVE)
   DEALLOCATE(PFMINSAVE)
   DEALLOCATE(ETSSAVE)
   DEALLOCATE(KPLUSSAVE)
   DEALLOCATE(KMINUSSAVE)
   DEALLOCATE(TOPPOINTERSAVE)
   DEALLOCATE(PLUSSAVE)
   DEALLOCATE(MINUSSAVE)
   DEALLOCATE(POINTERMSAVE)
   DEALLOCATE(POINTERPSAVE)
   DEALLOCATE(MINGROUP)
   DEALLOCATE(LOCATIONASAVE)
   DEALLOCATE(LOCATIONBSAVE)
   DEALLOCATE(NCONNSAVE)
   DEALLOCATE(FVIBMINSAVE,HORDERMINSAVE,IXMINSAVE,IYMINSAVE,IZMINSAVE,GPFOLDSAVE)
   DEALLOCATE(MINMAP)
   TEMPERATURE=TEMPSAVE
   STOP
ENDIF
GOTO 20

RETURN 

END SUBROUTINE SHANNON


